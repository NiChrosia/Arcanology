//file:noinspection all
plugins {
    id 'fabric-loom'
    id 'maven-publish'
    id "org.jetbrains.kotlin.jvm"
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version as Object
group = project.maven_group as Object

repositories {
    maven {
        name = "Technici4n"
        url = "https://raw.githubusercontent.com/Technici4n/Technici4n-maven/master/"
        content {
            includeGroup "net.fabricmc.fabric-api" // until PR # is merged
            includeGroup "dev.technici4n"
        }
    }

    maven {
        name = "BuildCraft"
        url = "https://mod-buildcraft.com/maven"
    }

    maven {
        name = "Patchouli"
        url = "https://maven.blamejared.com"
        content {
            includeGroup "vazkii.patchouli"
        }
    }

    maven {
        url = uri("https://storage.googleapis.com/devan-maven/")
        // for 0.4.2 and older
        // url uri("https://raw.githubusercontent.com/Devan-Kerman/Devan-Repo/master/")
    }

    maven {
        name = "CottonMC"
        url = "https://server.bbkr.space/artifactory/libs-release"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation include("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")
    modImplementation include("net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}")
    modImplementation include("net.devtech:arrp:${project.arrp_version}")

    modApi include("dev.technici4n:FastTransferLib:${project.ftl_version}")
    modImplementation include("vazkii.patchouli:Patchouli:${project.patchouli_version}")
    modImplementation include("io.github.cottonmc:LibGui:${project.libgui_version}")
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {
    it.options.encoding = "UTF-8"

    it.options.release = 16
}

java {
    withSourcesJar()
}

jar {
    from ("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}

compileKotlin.kotlinOptions.jvmTarget = "16"